#!groovy

/**
 * This is noggin-messages' Jenkins Pipeline Jenkinsfile.
 *
 * You can read documentation about this file at https://jenkins.io/doc/book/pipeline/jenkinsfile/.
 * A useful list of plugins can be found here: https://jenkins.io/doc/pipeline/steps/.
 *
 * For reference, this is the source of the fedoraInfraTox() macro that runs
 * tox on mutiple Fedora and CentOS releases:
 * https://github.com/centosci/cico-shared-library/blob/master/vars/fedoraInfraTox.groovy
 */

// fedoraInfraTox{}

/**
 * Distros we want to test on
 */
//def ACTIVE_DISTROS = ["f31", "f32", "epel7", "rawhide"]
def ACTIVE_DISTROS = ["f33", "f34", "epel7", "latest"]




def stages = [:]
def containers = [
                containerTemplate(name: 'jnlp',
                                  image: "quay.io/centosci/cico-workspace:latest",
                                  ttyEnabled: false,
                                  args: '${computer.jnlpmac} ${computer.name}',
                                  workingDir: "/workdir")
]

ACTIVE_DISTROS.each { distro ->
    stages["tox-${distro}"] = {
        stage("tox-${distro}"){
            container("${distro}"){
                sh "mkdir -p /workdir/home/${distro}"
                withEnv(["HOME=/workdir/home/${distro}"]) {
                    sh "cp -al ./ ../${distro}/"
                    dir( "../${distro}" ){
                        sh "rm -rf .tox"
                        try {
                            sh "tox --skip-missing-interpreters"
                            githubNotify context: "CI on ${distro}", status: 'SUCCESS', credentialsId: 'status-push-cred'
                        } catch(error) {
                            githubNotify context: "CI on ${distro}", status: 'FAILURE', credentialsId: 'status-push-cred'
                            throw error
                        }
                    }
                }
            }
        }
    }

    containers.add(containerTemplate(name: "${distro}",
                                            image: "quay.io/centosci/python-tox:${distro}",
                                            ttyEnabled: true,
                                            alwaysPullImage: true,
                                            command: "cat",
                                            workingDir: '/workdir'))
}

podTemplate(name: 'fedora-tox',
            label: 'fedora-tox',
            cloud: 'openshift',
            containers: containers
){
    node('fedora-tox'){
        ansiColor('xterm'){
            stage ('checkout'){
                checkout scm
            }

            parallel stages
        }
    }
}
